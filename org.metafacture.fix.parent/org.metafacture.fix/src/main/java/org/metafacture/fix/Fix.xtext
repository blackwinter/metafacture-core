grammar org.metafacture.fix.Fix with org.eclipse.xtext.xbase.Xbase
generate fix "http://www.metafacture.org/fix/Fix"

Fix:
    elements+=Expression*;

Expression :
    If | Do | MethodCall | {Expression} SL_COMMENT
;

If: 'if' MethodCall BEGIN
    elements+=Expression*
END
elseIf=ElsIf?
else=Else?
'end';

ElsIf: 'elsif' MethodCall BEGIN
	elements+=Expression*
END;

Else: {Else} 'else' BEGIN
	elements+=Expression*
END;

Do: 'do' MethodCall BEGIN
    elements+=Expression*
END
'end';

MethodCall:
    name=ValidID '('(params+=QualifiedName 
        (',' params+=QualifiedName)*)?')';

terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';

@Override 
terminal SL_COMMENT: 
	'#' !('\r'|'\n')* ('\r'? '\n');
