plugins {
	id 'com.adarshr.test-logger' version '1.7.0'
	id 'maven'
}

dependencies {
	testCompile 'org.junit.jupiter:junit-jupiter-api'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine'
	testCompile "org.eclipse.xtext:org.eclipse.xtext.testing:${xtextVersion}"
	testCompile "org.eclipse.xtext:org.eclipse.xtext.xbase.testing:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase:${xtextVersion}"
	implementation 'org.metafacture:metamorph:5.0.1'
	implementation 'org.metafacture:metafacture-commons:5.0.1'
	implementation 'org.metafacture:metafacture-mangling:5.0.1'
	testImplementation 'org.mockito:mockito-core:2.27.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:2.27.0'
}
configurations {
	mwe2 {
		extendsFrom compile
	}
}

dependencies {
	mwe2 'org.eclipse.emf:org.eclipse.emf.mwe2.launch'
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}"
	mwe2 "org.eclipse.xtext:org.eclipse.xtext.xtext.generator:${xtextVersion}"
	mwe2 'org.eclipse.xtext:xtext-antlr-generator'
}

task generateXtextLanguage(type: JavaExec) {
	main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
	classpath = configurations.mwe2
	inputs.file "src/main/java/org/metafacture/fix/GenerateFix.mwe2"
	inputs.file "src/main/java/org/metafacture/fix/Fix.xtext"
	outputs.dir "src/main/xtext-gen"
	args += "src/main/java/org/metafacture/fix/GenerateFix.mwe2"
	args += "-p"
	args += "rootPath=/${projectDir}/.."
}

test {
	useJUnitPlatform()
}
generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
eclipse.classpath.plusConfigurations += [configurations.mwe2]
